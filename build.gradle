/*
 * Root project for xtext-idea.
 */

buildscript {
	apply from: "${rootDir}/gradle/versions.gradle"
	repositories.jcenter()
	dependencies {
		classpath "org.xtext:xtext-gradle-plugin:$versions.xtext_gradle_plugin"
		classpath "io.spring.gradle:dependency-management-plugin:$versions.dependency_management_plugin"
	}
}

plugins {
  id 'org.ajoberstar.grgit' version '2.2.0'
}

apply from: "${rootDir}/gradle/versions.gradle"
apply from: "${rootDir}/gradle/bootstrap-setup.gradle"

subprojects {
	group = 'org.eclipse.xtext'
	version = rootProject.version
	
	apply plugin: 'java'
	apply plugin: "io.spring.dependency-management"
	dependencyManagement {
		imports {
			mavenBom "org.eclipse.xtext:xtext-dev.bom:$project.version"
		}
	}
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	
	apply from: "${rootDir}/gradle/upstream-repositories.gradle"
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/eclipse-project-layout.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories'
	delete 'build'
}
